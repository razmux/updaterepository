//===== rAthena Script ======================================= 
//= BattleGround System - Flavius
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.7
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Flavius Battleground.
//= - Winning Team: 9 badges (11 if VIP)
//= - Losing Team: 3 badges (5 if VIP)
//===== Additional Comments: ================================= 
//= 1.0 First Version.
//= 1.1 Fixed pink crystal spawning as blue. [L0ne_W0lf]
//= 1.2 Updated 'waitingroom' to support required zeny/lvl. [Kisuka]
//= 1.3 Removed MaxLvl check in waitingrooms. Replaced setwall with setcell.
//= 1.4 Label standardization. [Euphy]
//= 1.5 Added GM management function. [Euphy]
//= 1.6 Added VIP features and created a reward function. [Euphy]
//= 1.7 Made Crystals immune to attacks until Guardians are defeated. [Cydh/Aleos]
//============================================================

// Flavius Battleground Engine
//============================================================
bat_b01,15,15,3	script	start#bat_b01	844,{
OnInit:
	mapwarp "bat_b01","bat_room",154,150;
	end;

OnBuildTeams:
	// Rouge
	set $@BG_Team1,bg_team_create("bat_b01",10,290,1,"start#bat_b01::OnGuillaumeQuit","",$Palette_BG_Rouge);
	// Bleu
	set $@BG_Team2,bg_team_create("bat_b01",390,10,0,"start#bat_b01::OnCroixQuit","",$Palette_BG_Bleu);	
	end;
	
OnReset:
	donpcevent "OBJ#bat_b01_a::OnKill";
	donpcevent "OBJ#bat_b01_a::OnEnable";
	donpcevent "OBJ#bat_b01_b::OnKill";
	donpcevent "OBJ#bat_b01_b::OnEnable";
	donpcevent "guardian#bat_b01_a::OnKill";
	donpcevent "guardian#bat_b01_b::OnKill";
	donpcevent "guardian#bat_b01_a::OnEnable";
	donpcevent "guardian#bat_b01_b::OnEnable";
	donpcevent "cell#bat_b01_a::OnRed";
	donpcevent "cell#bat_b01_b::OnRed";
	donpcevent "time#bat_b01::OnEnable";
	disablenpc "Vintenar#bat_b01_aover";
	disablenpc "Vintenar#bat_b01_bover";
	end;

OnReady:
	if( getvariableofnpc(.BG_Arena$,"BG_Queue_Join") != "start#bat_b01" )
		end;
	if( $@FlaviusBG1 )
		end;	
	set $@FlaviusBG1, 1;
	set $@Guill_ScoreBG1, 0;
	set $@Croix_ScoreBG1, 0;
	set $@FlaviusBG1_Victory, 0;
	bg_updatescore "bat_b01",$@Guill_ScoreBG1,$@Croix_ScoreBG1;
	donpcevent "start#bat_b01::OnReset";
	bg_warp $@BG_Team1,"bat_b01",87,75;
	bg_warp $@BG_Team2,"bat_b01",311,224;
	end;
	
OnGuillaumeQuit:
OnCroixQuit:
	setquest 8506; // Deserter
	if( $@BG_Status != 0 && getvariableofnpc(.BG_Arena$,"BG_Queue_Join") == "start#bat_b01" )
		donpcevent "BG_Queue_Join::OnJoinEvent";
	end;
OnPrematureEnd:
	mapannounce "bat_b01.gat", "End of the game because not enough players !",bc_blue;
	$@BG_Status = 2;
	sleep 2000;
OnEnd:
	if($@FlaviusBG1_Victory == 2)
		callfunc("Reward_BG",3,9);
	else if($@FlaviusBG1_Victory == 1)
		callfunc("Reward_BG",9,3);
	else
		callfunc("Reward_BG",6,6);
	donpcevent "BG_Queue_Join::OnRotate";
	end;
}

bat_b01,1,1,3	script	OBJ#bat_b01_a	844,{
OnEnable:
	$@FlavBG1_C1_ID = bg_monster($@BG_Team1,"bat_b01",61,150,"Pink Crystal",1915,"OBJ#bat_b01_a::OnMyMobDead");
	setunitdata $@FlavBG1_C1_ID,UMOB_DMGIMMUNE,1; // Make Crystal immune to damage until Guardians are defeated
	end;

OnKill:
	killmonster "bat_b01","OBJ#bat_b01_a::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_b01","OBJ#bat_b01_a::OnMyMobDead") < 1) {
		mapannounce "bat_b01", "Guillaume's Crystal has been destroyed.",bc_map,"0xFFCE00";
		if ($@Croix_ScoreBG1 > 0) {
			set $@FlaviusBG1_Victory,2;
			set $@Croix_ScoreBG1,$@Croix_ScoreBG1+1;
			bg_updatescore "bat_b01",$@Guill_ScoreBG1,$@Croix_ScoreBG1;
			donpcevent "time#bat_b01::OnStop";
			$@BG_Status = 2;
			sleep 5000;
			donpcevent "start#bat_a01::OnEnd";
		}
		else {
			set $@Croix_ScoreBG1,1;
			donpcevent "time#bat_b01::OnEnable";
			donpcevent "start#bat_b01::OnReset";
			donpcevent "#bat_b01_timer::OnStop";
			bg_updatescore "bat_b01",$@Guill_ScoreBG1,$@Croix_ScoreBG1;
			bg_warp $@BG_Team1,"bat_b01",87,75;
			bg_warp $@BG_Team2,"bat_b01",311,224;
			donpcevent "#bat_b01_timer::OnEnable";
		}
	}
	end;
}

bat_b01,1,2,3	script	OBJ#bat_b01_b	844,{
OnEnable:
	$@FlavBG1_C2_ID = bg_monster($@BG_Team2,"bat_b01",328,150,"Blue Crystal",1914,"OBJ#bat_b01_b::OnMyMobDead");
	setunitdata $@FlavBG1_C2_ID,UMOB_DMGIMMUNE,1; // Make Crystal immune to damage until Guardians are defeated
	end;

OnKill:
	killmonster "bat_b01","OBJ#bat_b01_b::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_b01","OBJ#bat_b01_b::OnMyMobDead") < 1) {
		mapannounce "bat_b01", "Croix's Crystal has been destroyed.",bc_map,"0xFFCE00";
		if ($@Guill_ScoreBG1 > 0) {
			set $@FlaviusBG1_Victory,1;
			set $@Guill_ScoreBG1,$@Guill_ScoreBG1+1;
			bg_updatescore "bat_b01",$@Guill_ScoreBG1,$@Croix_ScoreBG1;
			donpcevent "time#bat_b01::OnStop";
			$@BG_Status = 2;
			sleep 5000;
			donpcevent "start#bat_a01::OnEnd";
		}
		else {
			set $@Guill_ScoreBG1,1;
			donpcevent "time#bat_b01::OnEnable";
			donpcevent "start#bat_b01::OnReset";
			donpcevent "#bat_b01_timer::OnStop";
			bg_warp $@BG_Team1,"bat_b01",87,75;
			bg_warp $@BG_Team2,"bat_b01",311,224;
			donpcevent "#bat_b01_timer::OnEnable";
		}
	}
	end;
}

bat_b01,1,3,3	script	guardian#bat_b01_a	844,{
OnEnable:
	bg_monster $@BG_Team1,"bat_b01",108,159,"Guillaume Camp Guardian",1949,"guardian#bat_b01_a::OnMyMobDead";
	bg_monster $@BG_Team1,"bat_b01",108,141,"Guillaume Camp Guardian",1949,"guardian#bat_b01_a::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_b01","guardian#bat_b01_a::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_b01","guardian#bat_b01_a::OnMyMobDead") < 1) {
		donpcevent "cell#bat_b01_a::OnGreen";
		mapannounce "bat_b01", "The Guardian protecting Guillaume's Crystal has been slain.",bc_map,"0xFFCE00";
		setunitdata $@FlavBG1_C1_ID,UMOB_DMGIMMUNE,0; // Make Crystal damageable again
	}
	end;
}

bat_b01,1,3,3	script	guardian#bat_b01_b	844,{
OnEnable:
	bg_monster $@BG_Team2,"bat_b01",307,160,"Croix Camp Guardian",1949,"guardian#bat_b01_b::OnMyMobDead";
	bg_monster $@BG_Team2,"bat_b01",307,138,"Croix Camp Guardian",1949,"guardian#bat_b01_b::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_b01","guardian#bat_b01_b::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_b01","guardian#bat_b01_b::OnMyMobDead") < 1) {
		donpcevent "cell#bat_b01_b::OnGreen";
		mapannounce "bat_b01", "The Guardian protecting Croix's Crystal has been slain.",bc_map,"0xFFCE00";
		setunitdata $@FlavBG1_C2_ID,UMOB_DMGIMMUNE,0; // Make Crystal damageable again
	}
	end;
}

bat_b01,1,4,3	script	cell#bat_b01_a	844,{
OnRed:
	setcell "bat_b01",62,149,60,151,cell_basilica,1;
	setcell "bat_b01",62,149,60,151,cell_walkable,0;
	end;

OnGreen:
	setcell "bat_b01",62,149,60,151,cell_basilica,0;
	setcell "bat_b01",62,149,60,151,cell_walkable,1;
	end;
}

bat_b01,1,5,3	script	cell#bat_b01_b	844,{
OnRed:
	setcell "bat_b01",327,151,329,149,cell_basilica,1;
	setcell "bat_b01",327,151,329,149,cell_walkable,0;
	end;

OnGreen:
	setcell "bat_b01",327,151,329,149,cell_basilica,0;
	setcell "bat_b01",327,151,329,149,cell_walkable,1;
	end;
}

bat_b01,1,6,1	script	time#bat_b01	844,{
OnEnable:
	donpcevent "Battle Therapist#b01_a::OnEnable";
	donpcevent "Battle Therapist#b01_b::OnEnable";
	end;

OnStop:
	donpcevent "Battle Therapist#b01_a::OnStop";
	donpcevent "Battle Therapist#b01_b::OnStop";
	end;
}

bat_b01,10,294,3	script	Battle Therapist#b01_a	95,{
	specialeffect2 EF_HEAL;
	mes "[Battle Therapist]";
	mes "Just close your eyes,";
	mes "and take a deep breath.";
	mes "You can be free from pain.";
	close;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	enablenpc "bat_b01_rp1_a_warp";
	end;

OnTimer26000:
	disablenpc "bat_b01_rp1_a_warp";
	end;

OnTimer26500:
	stopnpctimer;
	donpcevent "Battle Therapist#b01_a::OnEnable";
	end;

OnEnable:
	initnpctimer;
	enablenpc "Battle Therapist#b01_a";
	end;

OnStop:
	disablenpc "bat_b01_rp1_a_warp";
	disablenpc "Battle Therapist#b01_a";
	stopnpctimer;
	end;
}

bat_b01,10,290,0	script	bat_b01_rp1_a_warp	45,10,10,{
OnInit:
	disablenpc "bat_b01_rp1_a_warp";
	end;

OnTouch_:
	percentheal 100,100;
	warp "bat_b01",87,73;
	end;
}

bat_b01,389,14,3	script	Battle Therapist#b01_b	95,{
	specialeffect2 EF_HEAL;
	mes "[Battle Therapist]";
	mes "Just close your eyes,";
	mes "and take a deep breath.";
	mes "You can be free from pain.";
	close;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	enablenpc "bat_b01_rp1_b_warp";
	end;

OnTimer26000:
	disablenpc "bat_b01_rp1_b_warp";
	end;

OnTimer26500:
	stopnpctimer;
	donpcevent "Battle Therapist#b01_b::OnEnable";
	end;

OnEnable:
	initnpctimer;
	enablenpc "Battle Therapist#b01_b";
	end;

OnStop:
	disablenpc "bat_b01_rp1_b_warp";
	disablenpc "Battle Therapist#b01_b";
	stopnpctimer;
	end;
}

bat_b01,389,10,0	script	bat_b01_rp1_b_warp	45,9,9,{
OnInit:
	disablenpc "bat_b01_rp1_a_warp";
	end;

OnTouch:
	percentheal 100,100;
	warp "bat_b01",312,225;
	end;
}

bat_b01,1,5,3	script	countdown#bat_b01	844,{
OnInit:
	stopnpctimer;
	end;

OnEnable:
	stopnpctimer;
	initnpctimer;
	end;

OnStop:
	stopnpctimer;
	end;

OnTimer7000:
	mapannounce "bat_b01", "Guillaume Vintenar Axl Rose : Let's attack to destroy that Crystal!",bc_map,"0xFF9900";
	end;

OnTimer8000:
	mapannounce "bat_b01", "Croix Vintenar Swandery : Even though Guillaume is struggling to win against us, the victory is ours. Let's show them our power.",bc_map,"0xFF99CC";
	end;

OnTimer1800000:
	mapannounce "bat_b01", "Marollo VII : Guillaume Marollo, Croix Marollo! And their followers!",bc_map,"0x99CC00";
	end;

OnTimer1803000:
	mapannounce "bat_b01", "Marollo VII : Both camps are competitive, so it's hard to judge which team is superior.",bc_map,"0x99CC00";
	end;

OnTimer1808000:
	mapannounce "bat_b01", "Marollo VII : This battle of Flavian is such a waste of time. I will decide victory and defeat by your progress.",bc_map,"0x99CC00";
	end;

OnTimer1822000:
	mapannounce "bat_b01", "Marollo VII : If you can't accept the results, try again in another valley battle!",bc_map,"0x99CC00";
	end;

OnTimer1825000:
	mapannounce "bat_b01", "Axl Rose, Swandery : Yes, sir.",bc_map,"0x99CC00";
	end;

OnTimer1830000:
	$@BG_Status = 2;
	donpcevent "time#bat_b01::OnStop";
	end;

OnTimer1900000:
	donpcevent "countdown#bat_b01::OnStop";	
	donpcevent "start#bat_a01::OnEnd";	
	end;
}

bat_b01,81,83,3	script	Guillaume Camp#flag21	973,{ end; }
bat_b01,94,83,3	script	Guillaume Camp#flag22	973,{ end; }
bat_b01,81,66,3	script	Guillaume Camp#flag23	973,{ end; }
bat_b01,94,66,3	script	Guillaume Camp#flag24	973,{ end; }
bat_b01,139,142,3	script	Guillaume Camp#flag25	973,{ end; }
bat_b01,139,158,3	script	Guillaume Camp#flag26	973,{ end; }
bat_b01,110,161,3	script	Guillaume Camp#flag27	973,{ end; }
bat_b01,110,137,3	script	Guillaume Camp#flag28	973,{ end; }
bat_b01,63,135,3	script	Guillaume Camp#flag29	973,{ end; }
bat_b01,63,165,3	script	Guillaume Camp#flag30	973,{ end; }
bat_b01,10,296,3	script	Guillaume Camp#flag31	973,{ end; }

bat_b01,306,233,3	script	Croix Camp#flag21	974,{ end; }
bat_b01,317,233,3	script	Croix Camp#flag22	974,{ end; }
bat_b01,306,216,3	script	Croix Camp#flag23	974,{ end; }
bat_b01,317,216,3	script	Croix Camp#flag24	974,{ end; }
bat_b01,257,158,3	script	Croix Camp#flag25	974,{ end; }
bat_b01,257,141,3	script	Croix Camp#flag26	974,{ end; }
bat_b01,297,164,3	script	Croix Camp#flag27	974,{ end; }
bat_b01,297,136,3	script	Croix Camp#flag28	974,{ end; }
bat_b01,336,161,3	script	Croix Camp#flag29	974,{ end; }
bat_b01,336,139,3	script	Croix Camp#flag30	974,{ end; }
bat_b01,389,16,3	script	Croix Camp#flag31	974,{ end; }

bat_b01	mapflag	battleground	2
bat_b01	mapflag	nomemo
bat_b01	mapflag	nosave	SavePoint
bat_b01	mapflag	noteleport
bat_b01	mapflag	nowarp
bat_b01	mapflag	nowarpto
bat_b01	mapflag	noreturn
bat_b01	mapflag	nobranch
bat_b01	mapflag	nopenalty
bat_b01	mapflag	nodrop
